plugins {
	id 'org.springframework.boot' version '2.6.2'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id "jacoco"
	id "org.sonarqube" version "3.3"
	id 'java'
}

group = 'com.abnamro'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

ext {
	springBootVersion = '2.6.2'
	springFrameworkVersion = '5.5.1'
	testContainersVersion = '1.16.2'
	lombokVersion = '1.18.22'
	mapStructVersion = '1.4.2.Final'
	springDocOpenApiVersion = '1.6.4'
	javaJwtVersion = '3.18.3'
}

dependencies {
	// Spring
	implementation "org.springframework.boot:spring-boot-starter-data-jpa:${springBootVersion}"
	implementation "org.springframework.boot:spring-boot-starter-security:${springBootVersion}"
	implementation "org.springframework.boot:spring-boot-starter-validation:${springBootVersion}"
	implementation "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"
	developmentOnly "org.springframework.boot:spring-boot-devtools:${springBootVersion}"
	testImplementation "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"
	testImplementation "org.springframework.security:spring-security-test:${springFrameworkVersion}"

	// Database
	runtimeOnly 'com.h2database:h2:2.0.206'
	runtimeOnly 'mysql:mysql-connector-java:8.0.25'

	// Test Containers
	testImplementation "org.testcontainers:junit-jupiter:${testContainersVersion}"
	testImplementation "org.testcontainers:mysql:${testContainersVersion}"

	// Lombok
	compileOnly "org.projectlombok:lombok:${lombokVersion}"
	annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

	// MapStruct
	implementation "org.mapstruct:mapstruct:${mapStructVersion}"
	annotationProcessor "org.mapstruct:mapstruct-processor:${mapStructVersion}"
	testAnnotationProcessor "org.mapstruct:mapstruct-processor:${mapStructVersion}"

	// Springdoc
	implementation "org.springdoc:springdoc-openapi-ui:${springDocOpenApiVersion}"
	implementation "org.springdoc:springdoc-openapi-data-rest:${springDocOpenApiVersion}"

	// JWT
	implementation "com.auth0:java-jwt:${javaJwtVersion}"
}

tasks.withType(JavaCompile) {
	options.compilerArgs = [
			'-Amapstruct.defaultComponentModel=spring',
			'-Amapstruct.defaultInjectionStrategy=constructor',
			'-Amapstruct.unmappedTargetPolicy=ERROR'
	]
}

dependencyManagement {
	imports {
		mavenBom "org.testcontainers:testcontainers-bom:${testContainersVersion}"
	}
}

test {
	useJUnitPlatform()
	finalizedBy jacocoTestReport // report is always generated after tests run
}

jacocoTestReport {
	reports {
		xml.enabled true
	}
	dependsOn test // tests are required to run before generating the report
}

sonarqube {
	properties {
		property "sonar.host.url", "https://sonarcloud.io"
		property "sonar.organization", "edisonfillus-github"
		property "sonar.projectKey", "abn-amro"
		property "sonar.projectName", "ABN Amro"
		property "sonar.projectVersion", "${version}"
		property "sonar.jacoco.reportPath", "$buildDir/jacoco/test.exec"
		property "sonar.junit.reportsPath", "$buildDir/test-results/*/TEST-*.xml"
	}
}

tasks.named('sonarqube').configure {
	dependsOn test
}

bootRun {
	args = ["--spring.profiles.active=local"] // bootRun execute always in local mode (h2)
}
